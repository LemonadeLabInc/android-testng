// Build dependencies
buildscript {
  repositories {
    maven { url 'https://maven.google.com' }
    mavenCentral()
    google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.4'
  }
}

plugins {
  id 'de.lemona.gradle' version '0.3.13'
}

// Plugins
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

// Android setup
android {
  compileSdkVersion 30

  // Instrumentation runner defined here, we publish this...
  defaultConfig {
    minSdkVersion 22
    targetSdkVersion 30
    testInstrumentationRunner 'de.lemona.android.testng.TestNGRunner'
  }

}

// Ignore test failures, we have forced a couple of them
gradle.taskGraph.whenReady {
  connectedDebugAndroidTest {
    ignoreFailures = true
  }
}

// Our dependencies
dependencies {

  // Depend on TestNG (obviously)
  api('org.testng:testng:6.8.21') {
   exclude group: 'com.beust', module: 'jcommander'
   exclude group: 'org.beanshell', module: 'bsh'
   exclude group: 'junit', module: 'junit'
  }

  // Guice required for compilation (provided) and test
  compileOnly('com.google.inject:guice:3.0:no_aop') {
    exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    exclude group: 'aopalliance', module: 'aopalliance'
  }

  androidTestImplementation('com.google.inject:guice:3.0:no_aop') {
    exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    exclude group: 'aopalliance', module: 'aopalliance'
  }
}