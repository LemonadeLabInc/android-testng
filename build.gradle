// Import the Android plugin
buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.1'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
  }
}

// Group and version
group = 'de.lemona.android'
version = '1.0'

// Local plugins
apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

// Shared configurations
apply from: 'http://lemonadelabinc.github.io/gradle-snippets/android.gradle'
apply from: 'http://lemonadelabinc.github.io/gradle-snippets/eclipse.gradle'
apply from: 'http://lemonadelabinc.github.io/gradle-snippets/maven.gradle'

// Android setup
android {
  compileSdkVersion 22
  buildToolsVersion '22.0.1'

  // Instrumentation runner defined here, we publish this...
  defaultConfig {
    testInstrumentationRunner 'de.lemona.android.testng.TestNGRunner'
  }
}

// Ignore test failures, we have forced a couple of them
project.gradle.taskGraph.whenReady {
  connectedDebugAndroidTest {
    ignoreFailures = true
  }
}

// Remove our "guice" dependency from the POM
afterEvaluate {
  def _dependencies = publishing.publications.main.runtimeDependencies
  _dependencies.each { _dep ->
    if (_dep.groupId == 'com.google.inject') {
      _dependencies.remove(_dep)
    }
  }
}

// Our dependencies
dependencies {
  compile('org.testng:testng:6.8.21') {
   exclude group: 'com.beust', module: 'jcommander'
   exclude group: 'org.beanshell', module: 'bsh'
   exclude group: 'junit', module: 'junit'
  }

  compile('com.google.inject:guice:3.0:no_aop') {
    exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    exclude group: 'aopalliance', module: 'aopalliance'
  }
}

/* ========================================================================== */
/* Publish to Bintray if we have to                                           */
/* ========================================================================== */

def _bintrayUser  = System.getenv("BINTRAY_USER")
def _bintrayKey   = System.getenv("BINTRAY_KEY")

if ((_bintrayUser == null) && hasProperty('bintray.user')) _bintrayUser = ext['bintray.user']
if ((_bintrayKey  == null) && hasProperty('bintray.key'))  _bintrayKey  = ext['bintray.key']

if ((_bintrayUser != null) && (_bintrayKey != null)) {
  def _version = version // save for the closure below
  bintray {
    user = _bintrayUser
    key = _bintrayKey
    publications = ['main']
    publish = true
    pkg {
      repo = 'maven'
      name = 'android-testng'
      userOrg = 'lemonade'
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/LemonadeLabInc/android-testng/'
      version { name = _version }
    }
  }
}
