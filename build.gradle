// Build dependencies
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.1'
  }
}

plugins {
  id 'de.lemona.gradle' version '0.0.0'
  id 'com.jfrog.bintray' version '1.3.1'
}

// Plugins
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

// Android setup
android {
  compileSdkVersion 22
  buildToolsVersion '22.0.1'

  // Instrumentation runner defined here, we publish this...
  defaultConfig {
    minSdkVersion 22
    targetSdkVersion 22
    testInstrumentationRunner 'de.lemona.android.testng.TestNGRunner'
  }
}

// Ignore test failures, we have forced a couple of them
gradle.taskGraph.whenReady {
  connectedDebugAndroidTest {
    ignoreFailures = true
  }
}

// Our dependencies
dependencies {

  // Depend on TestNG (obviously)
  compile('org.testng:testng:6.8.21') {
   exclude group: 'com.beust', module: 'jcommander'
   exclude group: 'org.beanshell', module: 'bsh'
   exclude group: 'junit', module: 'junit'
  }

  // Guice required for compilation (provided) and test
  provided('com.google.inject:guice:3.0:no_aop') {
    exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    exclude group: 'aopalliance', module: 'aopalliance'
  }

  androidTestCompile('com.google.inject:guice:3.0:no_aop') {
    exclude group: 'org.sonatype.sisu.inject', module: 'cglib'
    exclude group: 'aopalliance', module: 'aopalliance'
  }
}

// Bintray extra configuration
bintray {
  pkg {
    name = 'android-testng' // TODO: remove me when we have time
    userOrg = 'lemonade'
    licenses = [ 'Apache-2.0' ]
    vcsUrl = 'https://github.com/LemonadeLabInc/android-testng/'
  }
}
